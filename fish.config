# ______ _____ _____ _   _
# |  ___|_   _/  ___| | | |
# | |_    | | \ `--.| |_| |
# |  _|   | |  `--. \  _  |
# | |    _| |_/\__/ / | | |
# \_|    \___/\____/\_| |_/
#  _____ _____ _   _______ _____ _____ 
# /  __ \  _  | \ | |  ___|_   _|  __ \
# | /  \/ | | |  \| | |_    | | | |  \/
# | |   | | | | . ` |  _|   | | | | __ 
# | \__/\ \_/ / |\  | |    _| |_| |_\ \
#  \____/\___/\_| \_|_|    \___/ \____/
# 
# Configuration file for the Friendly Interactive SHell.
#
# References:
# - https://fishshell.com/docs/current/index.html

# Environment Variables {{{

set --global --export EDITOR /usr/bin/nvim
set --global --export GOPATH /home/max/go/
set --global --export JAVA_HOME /usr/lib/jvm/java-8-openjdk-amd64/
set --global --export PATH /usr/local/sbin:/usr/local/bin:/usr/bin:/usr/lib/jvm/default/bin:/usr/bin/site_perl:/usr/bin/vendor_perl:/usr/bin/core_perl:/bin:/usr/games:/home/max/.local/bin:/usr/sbin:/sbin:/home/max/go/bin:/home/max/bin:/snap/bin:/home/max/.cargo/bin:/home/max/.yarn/bin
set --global --export SHELL /usr/bin/fish
set --global --export TERM xterm-256color

# Disable the intro message
set fish_greeting

# }}}
# Abbreviations {{{
# Each abbreviation gets automatically expanded from its short form to
# a full form. This is great for things like git: Where I want to see
# the full command before hitting enter, but I also want a quick way
# to access common commands.

abbr --add --global gstat git status
abbr --add --global gcomm git commit
abbr --add --global gpush git push
abbr --add --global gpull git pull
abbr --add --global gfetch git fetch
abbr --add --global gadd git add
abbr --add --global gdiff git diff
abbr --add --global glog git log
abbr --add --global gcheck git checkout
abbr --add --global gcheckb git checkout
abbr --add --global gbranch git branch

# }}}
# Bookmarks {{{
# Bookmarks are global variables that are prefixed with "bk_". The
# purpose of bookmarks are to store directories that I need frequent
# access to. By just evoking the variable directly, I will jump
# directly the the stored directory. I can also use these bookmarks
# as part of another command. For example, to edit the nvim configuration
# file inside my dotfiles directory: `vim $bk_dotfiles/nvim.config`.

set --global bk_camh $HOME/git/camh/
set --global bk_dotfiles $HOME/git/dotfiles/
set --global bk_golang $GOPATH
set --global bk_pdf $HOME/.nextcloud/doc/pdf/

# }}}
# Functions {{{
# These functions act as Bash aliases. Unlike abbreviations, I do not
# want to see the expanded version of these commands. Therefore these
# funcitons should not have any major side-effects.

# ls {{{
# Functions for the ls command.

# Base ls
function ls
  /bin/ls --color=auto \
          --group-directories-first \
          --dereference \
          --human-readable \
          $argv
end

# Default
function l
  ls -C --ignore-backups $argv
end

# Default almost all
function la
  l --almost-all $argv
end

# List
function ll
  ls -l --classify $argv
end

# List almost all
function lla
  ll --almost-all $argv
end

# }}}
# Always use the colour output with grep.
function grep
  /bin/grep --color=auto $argv
end

# Quick terminal exit.
function q
  exit
end

# Run a full non-dist upgrade on Debian/Ubuntu. Cleans up useless
# packages afterwards.
function uu
  sudo apt update; and sudo apt upgrade; and sudo apt autoremove
end

# Run `tmux` with the `-f` flag to specify the custom location of the
# configuration file.
function tmux
  /usr/bin/tmux -f ~/.config/tmux/tmux.config $argv
end

# }}}
# Prompt {{{
# Custom shell prompt. Shows the current user and hostname, the current
# working directory, and the git information if applicable. The lambda
# prompt changes from green to red if the previous command returned a
# non-zero result.

function fish_prompt
  set -l lastexit $status
  set -l userhost (set_color yellow)$USER@(hostname):
  set -l workingdir (set_color blue)(prompt_pwd)
  set -l gitprompt (set_color --dim --italics white)(__fish_git_prompt)

  if test $lastexit -eq 0
    set symbol (set_color --bold green)λ\ 
  else
    set symbol (set_color --bold red)λ\ 
  end

  echo
  echo "$userhost""$workingdir""$gitprompt"
  echo "$symbol"(set_color white)
end

# }}}

# vim:foldmethod=marker:foldlevel=0:filetype=fish
